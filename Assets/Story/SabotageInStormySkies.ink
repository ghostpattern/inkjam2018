* So you know where this ship came from?

- Ah hah, you’d like to know would you?

Well, it washed up on these shores when you would’ve been just born.

Found by local folks like you and me.

But to be found, it must have once been lost. Lost at sea...

* I thought no one knew how it got lost?

- Correction! No one living knows…

We can find out!

But I’ll need your help.

* Um. Okay?

- Good lad. Now bring over those candles.

Yes, yes. Now, sit with me.

Hold out your hands, yes. Come on now, I don’t have cooties, hah!

Alright, now close your eyes…

Do you see it?

* See it?

- Scene: Load 1 // ship moored above town
Visual: FadeIn 1.0
Audio: FadeUp scene1_loop_rain 1


The ship as it once was, decades ago! Majestic and proud, flying above the harbour.

It was at the port city of..

Ah! They’re speaking to me..
Audio: FadeUp scene1_loop_town 1
Rio. Beautiful Rio de Janeiro, after the war.

It was, as it always is, a dark and stormy night.

Audio: Play lightning

The passengers and crew were all down in the town - it wasn’t safe up on the ship!

In fact, that’s what our heroes were counting on…

* Heroes? Like, superheroes?

- Visual: FadeOut 1.0
Audio: FadeDown scene1_loop_rain 1
Audio: FadeDown scene1_loop_town 1
Scene: Load 2 1.0

Yes, just like superheroes!

They are the ones speaking to us.
Visual: FadeIn 1.0
Audio: FadeUp scene2_loop_parachute 0.5

I see.. Two figures. Two small figures above the vast Pacific ocean, floating down amongst of the storm.

Audio: FadeUp scene2_loop_rainthunder 0.5

It’s some miracle the lightning doesn’t fry their parachutes, but they’ve risked death before.

Clad in black, these swashbuckling vagabonds fly grim-faced towards the massive blimp.

Audio: FadeDown scene2_loop_parachute 0.2
Audio: FadeDown scene2_loop_rainthunder 0.2
Audio: Play AARK 0.2

AARK!

* Are you okay?
* Did you burn yourself?

- Oh I’m fine dear, I’m fine!

That was just…

Audio: FadeUp scene2_loop_parachute 0.2
Audio: FadeUp scene2_loop_rainthunder 0.2

The parrot!

Visual: Show parrot 0.5

Yes, their faithful and trusty parrot, Kiddo, who accompanied them on every adventure.

Anyway!

They slammed into the balloon’s canvas, bouncing against it, the wind whipping their parachutes around..

Visual: FadeOut 0.1
Scene: Load 3 0.1
Audio: Play Slam

The deck came up to meet them and they slammed down hard.

Audio: FadeDown scene2_loop_parachute 0.2
Audio: FadeDown scene2_loop_rainthunder 0.2
Audio: FadeUp scene3_loop_rain 1
Visual: FadeIn 1.0

They struggled to their feet amidst the pouring rain, soaked and sopping wet, but determined.

Yes, nothing will stop them from completing their mission.

Or so they think..

* What was their mission?

- Well, step one, was to infiltrate the ship! They’d done that one by now.

Step two, was to steal the papers out of the candidate’s room.

Step three, was to get to the bridge, and to use the radio to broadcast their manifesto and list of demands!

And step four, if it was necessary, was to sever the cable - so that the ship, and everything on it, would blow away in the storm.

-> about_the_mission

=== about_the_mission ===

* Who were they?

Ahah, well. Do you know what an anarcho-syndicalist is?

* * No.
* * A what?
- - What do they teach you kids in school these days?

Ah it doesn’t matter.

All you need to know is these were two brave souls, freedom fighters against the growing global capitalist regime.

This luxurious vessel, at that time, was owned by one of the largest *con*-glomerates of the Pacific travel industry.

Tickets were a thousand pounds a pop, and wealth flowed from the pockets of the idle rich, up to the pockets of the powerful richest.

These heroes were fighting to dam that flow - to divorce the rich bourgeoisie from their playthings, and open up everyone’s eyes to the System.

	-> about_the_mission

 * Who was the candidate?

Ah, now he was one of the biggest bigwigs of all!

A dictator-in-the-making, but one that the heroes knew they could topple.

You see, the CIA at the time were putting puppet leaders in power all across South America - using lies and manipulation to undermine the will of the people.

Part of their paranoid attack against the Big Red Scare.

The election was coming up soon, and the Candidate’s victory was all but certain.

The heroes knew, however, that in his safe, in his room on the ship, were papers that would expose where his true allegiances lay.

-> about_the_mission

* Why did they want to destroy the ship?

	They were prepared to destroy it in the name of their values.

	You see, when you’re down and out.

	When you’re oppressed on every side by those in power.

	Sometimes you need to take drastic, irreversible action, to make a break in the system.

	If you end up on the right side of history, you’re a proud revolutionary, valiant against the tyrannical rule.

	Otherwise, you’re a dangerous terroriste, whose destruction was necessary for the continuity of the Great State.

	-> about_the_mission

* What happened next?

- Visual: FadeOut 0.5
Audio: FadeDown scene3_loop_rain 0.5
Audio: FadeUp scene45_loop_rain

They moved straight into action, and, finding a maintenance hatch, infiltrated the first class deck.

They were wary of being spotted, even though no one was meant to be on board, because--

* Because of the storm.

- Because of the storm! Yes, you’re exactly right.

They found the Candidate’s quarters and jimmied the lock, then slipped inside…

Scene: Load 4
Visual: FadeIn 1

There they were, the safe right before them.

But oh! How did they open it without the code? Their voices are getting faint..

Can you hear them? How did they do it?

-> the_code

=== the_code ===

+ They found it somewhere!

{Ah of course. But where did they search?|So where else did they search?}

	* * Under the bed.

		Audio: Play Mattress
		They pulled the mattress up, looked under every slat, but found nothing.

		-> the_code

* * In the desk.
	
	Audio: Play Desk
	They pulled open every drawer, finding note paper, fancy pens and inkwells (they used them back in those days.

	But no safe codes!

		-> the_code
	
	* * On the bookshelf.

	Audio: Play Booshelf
	They flicked through the every books, searching the margins for clues.

But other than discovering some dirty doodles, it was a fruitless endeavour. 

		-> the_code
		
	* * Under the chair.

		Audio: Play Chair
		What - you mean, under the seat?

		Nothing there, unfortunately!

		-> the_code
		
	* * Among his notes.

		Audio: Play Notes
		Ooh this might be the ticket…

		They went through his notes, and pocketed a few that had some salacious details on other foul characters of the bourgeoisie, but nope - no code!

		-> the_code
		
	* * In the vase.

		Audio: Play Vase
		They pulled out the flowers from the vase and examined each stem - maybe he’d absentmindedly carved the code one day on a flower-stem!

		But as likely as that would be, no code was forthcoming.

		-> the_code
		
	- - Visual: Open Safe

* They picked the lock!
	
	Audio: Play Safelock
	Ah yes you’re right - the smaller vagabond had learned the fine art of lockpicking as a child.. but that’s another story.

	She put her ear to the safe and felt for the tell-tale clicks…

	Visual: Animate Safe Safeopen
	Audio: Play Safeopen


	And in minutes she had it open - the papers were theirs!

- 
Visual: FadeOut 1

They rushed from the Candidate’s quarters to the bridge - it was time to hijack the air-ways.

They reached the captain’s deck, but the entryway to the bridge was barred!

Scene: Load 5
Visual: FadeIn 1

The door was heavy, solid steel - probably the heaviest thing on the ship other than the engines.

They weren’t getting through without some difficulty.

So they huddled together and decided.. oh, I can’t quite make them out again..

* They sent in the parrot!

What parrot?

Oh, right, the parrot! 

Audio: Play Ark

It had been with them the whole time of course, and, with a pat for luck from the bigger brigand, it climbed into a nearby vent.

Audio: Play Parrotvent
They heard it valiantly hop through the maze of ducts, squawking as it hit crossroads or dead ends..

The syndicalists stood there patiently for several minutes.

Visual: Animate Dooropen
Audio: Play Dooropen

But they knew they could trust the parrot.

- 
Visual: FadeOut 0.25
Scene: Load 6 0.25
Visual: FadeIn 0.25
Audio: FadeDown scene45_loop_rain 0.5
Audio: FadeUp scene6_loop_rain 0.5

They were in - they sat down with the manifesto in hand in front of the radio transmitter and prepared to broadcast.

But when they activated the transmitter, sparks jumped and fizzled - the device had been burnt to a crisp, by a bolt of lightning no doubt.
Audio: Play Radio

So, what would they do?

* Well, they couldn’t abandon the mission.

- Indeed, they couldn’t. They’d come too far, and they could still send their message.

They could still take a hammer and sickle to the system!

Visual: FadeOut 0.1
Scene: Load 7 0.1
Visual: FadeIn 0.1
Audio: FadeDown scene6_loop_rain 0.2
Audio: FadeUp scene7_loop_rainwind 0.2

They clambered down the side of the ship, lashed by the icy wind.

They got to the cable and, through some pretty impressive cooperative gymnastics, attached the explosive to the cable.

The lights of the town winked in the distance.

They lit the fuse…
Audio: FadeDown scene7_loop_rainwind 0.5
Visual: FadeOut 0.5

Audio: Play Explosion

 The severed cable whipped away into the night.

Scene: Load 8
Visual: FadeIn 0.5
Audio: FadeUp scene8_loop_rainstorm 0.5
The vagabonds clambered back on to the deck, their mission completed.

At this point, their pilot was meant to double back, and they would organise some kind of daring escape.

But the team knew once the bonds to the earth were cut, the ship would blow any which way through the storm.

But still, there was a chance.

So they waited, and they watched.

Visual: FadeOut 1.0
Scene: Load 9 1.0
Visual: FadeIn 1.0
Audio: FadeIn scene9_loop_thunder 0.2
Audio: FadeIn scene910_loop_wind 0.2
Audio: FadeDown scene8_loop_rainstorm 0.2

The ship drifted through the storm, through the clouds.

The rescue plane did not show up, and would not show up.

* So they just… died?

- Um well.

That’s what I’m hearing.

But! Not straight away of course.

Oh no.

Visual: FadeOut 0.1
Scene: Load 10 0.1
Visual: FadeIn 0.1
Audio: FadeDown scene9_loop_thunder 0.2

They in fact, could’ve lived for many years on the wayward vessel!

Audio: Play Gull
The food stores were stocked, and they, uh, learned to hunt seabirds! Seagulls and albatrosses and the like.

And the uh, the parrot was still around! It could scavenge for food - you know, brought up herbs and other plants.

No, they died many years later, of old age I suppose! A happily ever after.

Or perhaps, happy as you can be.

On an empty floating ship above the vastness of the oceans of the world.

* Why didn’t they take control of the ship and pilot it to safety?

* Wouldn’t the ship have floated over land - they could’ve made a rope and escaped!

* Why didn’t they have a better escape plan?

* Doesn’t the ship still have its cable attached?

- Visual: FadeOut 0.5

Oh uh - oh, no.. the candle’s blown out!

I’ve lost the connection, dear.

Sometimes that’s just how it goes, when communing with the spirits.

You just have to trust that they’ve told you everything you need to know.

* [But it doesn’t make sense!]

- But--

Now as my mother always said, butts are for goats.

You run along now.

It’s just about time for my nap.

Audio: FadeDown scene910_loop_wind 1

-> DONE
